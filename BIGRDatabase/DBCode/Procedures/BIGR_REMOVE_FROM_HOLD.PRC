CREATE OR REPLACE PROCEDURE BIGR_REMOVE_FROM_HOLD
	   (P_ARDAIS_ID VARCHAR2, P_ID_LIST VARCHAR2, P_DELIM VARCHAR2)
AS

   V_ARDAIS_ID		ES_ARDAIS_USER.ARDAIS_USER_ID%TYPE := P_ARDAIS_ID;
   V_ID_LIST		VARCHAR2(4000) := P_ID_LIST;
   V_DELIM			VARCHAR2(2) := p_delim;
   CUR_SAMPLE_ID	VARCHAR2(12);
   V_CUR_DELIM		NUMBER := 0;
   V_NEXT_DELIM		NUMBER := 0;
   V_EXISTING_ROWS	NUMBER := 0;

   INVALID_SAMPLE   EXCEPTION;

BEGIN
	V_NEXT_DELIM := GET_NEXT_DELIM (V_ID_LIST, V_DELIM, V_NEXT_DELIM);
	LOOP
	   EXIT WHEN V_CUR_DELIM = V_NEXT_DELIM;
	   CUR_SAMPLE_ID := SUBSTR(V_ID_LIST,V_CUR_DELIM+1,V_NEXT_DELIM - (V_CUR_DELIM + 1));

	   IF CUR_SAMPLE_ID LIKE 'RN%'
	   THEN
	      DELETE FROM ES_SHOPPING_CART_DETAIL WHERE BARCODE_ID = CUR_SAMPLE_ID
		  AND ARDAIS_USER_ID = V_ARDAIS_ID;
	   ELSIF CUR_SAMPLE_ID LIKE 'PA%' OR CUR_SAMPLE_ID LIKE 'FR%' OR CUR_SAMPLE_ID LIKE 'SX%'
	   THEN
	   	  --take this action only if the sample id is actually on the hold list
	   	  --for this user.  If it's not, the delete statement below would
	   	  --be harmless but inserting the record into iltds_sample_status
	   	  --could be harmful.
	   	  SELECT COUNT(1) INTO V_EXISTING_ROWS FROM ES_SHOPPING_CART_DETAIL 
	      	WHERE BARCODE_ID = CUR_SAMPLE_ID
		    AND ARDAIS_USER_ID = V_ARDAIS_ID;
	      IF V_EXISTING_ROWS >= 1 THEN
	      	DELETE FROM ES_SHOPPING_CART_DETAIL WHERE BARCODE_ID = CUR_SAMPLE_ID
		  	AND ARDAIS_USER_ID = V_ARDAIS_ID;
		  	INSERT INTO ILTDS_SAMPLE_STATUS (ID, SAMPLE_BARCODE_ID, SAMPLE_STATUS_DATETIME, STATUS_TYPE_CODE)
		  	VALUES (ILTDS_SAMPLE_STATUS_PK_SEQ.NEXTVAL, CUR_SAMPLE_ID, SYSDATE, 'GENRELEASED');
		  END IF;
		ELSE
		   RAISE INVALID_SAMPLE;
		END IF;
	      V_CUR_DELIM := V_NEXT_DELIM;
	      V_NEXT_DELIM := GET_NEXT_DELIM (V_ID_LIST, V_DELIM, V_NEXT_DELIM);
	END LOOP;

	EXCEPTION
	   WHEN INVALID_SAMPLE THEN
	      DBMS_OUTPUT.PUT_LINE ('Sample ' || CUR_SAMPLE_ID || ' is invalid.  Rolling back transaction.');
		  ROLLBACK;
	    WHEN OTHERS THEN
		   DBMS_OUTPUT.PUT_LINE ('An error occured: ' || SQLERRM);
END;
/
